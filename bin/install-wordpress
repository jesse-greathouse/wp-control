#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;
use JSON;
use File::Path qw(rmtree);
use File::Basename qw(dirname);
use Cwd qw(abs_path getcwd);
use LWP::UserAgent ();
use LWP::Simple;
use Term::Menus;
use Archive::Zip qw( :ERROR_CODES :CONSTANTS );

# Dynamically resolve paths
my $binDir = abs_path(dirname(__FILE__));
my $applicationRoot   = abs_path(dirname($binDir));
my $opt    = "$applicationRoot/opt";
my $web    = "$applicationRoot/web";
my $php    = "$opt/php/bin/php";

my $argnum;
my @versions;
my @versionData;
my $latestVersion;
my $versionEndpoint;
my $selectedVersion;
my $requestedVersion = undef;
my $versionCheckUri = "https://api.wordpress.org/core/version-check/1.7/";
my %versionDownloadTable;

GetOptions("version=s" => \$requestedVersion) or die("Error in command line arguments\n");

# Call API for version data.
@versionData = get_version_data();

# Parse the data and assign the latest version and versions list.
for my $i (0 .. $#versionData)
{
  if ($i == 0) {
    $latestVersion = $versionData[$i]->{'version'};
  } else {
    push(@versions, $versionData[$i]->{'version'});
    $versionDownloadTable{$versionData[$i]->{'version'}} = $versionData[$i]->{'download'};
  }
}

# Validate the requested version.
# Prompt the user for a version if none was defined.
if (defined $requestedVersion) {
  validate_requested_version($requestedVersion, @versions);
  $selectedVersion = $requestedVersion;
  print "Requested Version: $selectedVersion \n";
} else {
  $selectedVersion = &pick(\@versions,"  Choose your WordPress Version:");
  print "Selected Version: $selectedVersion \n";
}

download_package($versionDownloadTable{$selectedVersion}, $opt);

link_package($opt, $web);

update_wp_cli_bundle();

# ====================================
#    Subroutines below this point
# ====================================

# Returns API data as JSON.
sub get_version_data {
  my $ua = LWP::UserAgent->new(timeout => 10);
  my $response = $ua->get($versionCheckUri);

  if ($response->is_success) {
      my $decoded = JSON->new->utf8->decode($response->decoded_content);
      return @{$decoded->{'offers'}};
  } else {
      die $response->status_line;
  }
}

# Validate a requested version.
sub validate_requested_version {
  my ($requestedVersion, @versions) = @_;
  my %params = map { $_ => 1 } @versions;

  if(!exists($params{$requestedVersion})) {
    die "Requested version: $requestedVersion is not supported.";
  }
}

# Download the package
# Download the package
sub download_package {
  my ($url, $dir) = @_;

  print "Cleaning old WordPress archives in $dir...\n";
  clean_wordpress_archive($dir);

  my @spl = split("/", $url);
  my $fileName = $spl[@spl - 1];
  my $file = "$dir/$fileName";
  print "Preparing to download: $fileName from: $url \n";

  my $result = getstore($url, $file);
  if ($result == 200) {
    my $size = -s $file;
    if (defined $size && $size > 0) {
      print "Download successful: $file ($size bytes)\n";
    } else {
      die "Downloaded file is empty or unreadable: $file\n";
    }
  } else {
    die "Download failed with status code: $result\n";
  }

  print "Extracting package to $dir...\n";
  extract_package($file, $dir);
  print "Extraction complete.\n";
}

sub extract_package {
  my ($file, $dir) = @_;
  my $wordpressDir = "$dir/wordpress";

  # Read the zip file
  my $zip = Archive::Zip->new();
  unless ( $zip->read( $file ) == AZ_OK ) {
    die 'read error';
  }

  my $cwd = getcwd();

  if (-d $wordpressDir) {
    rmtree($wordpressDir) or die "Couldn't remove $wordpressDir directory, $!";
  }

  mkdir($wordpressDir) or die "Couldn't create $wordpressDir directory, $!";

  chdir($wordpressDir) or die "Couldn't go inside $dir directory, $!";

  $zip->extractTree('wordpress');

  # Remove wp-content folder.
  rmtree("$wordpressDir/wp-content") or die "Couldn't remove $wordpressDir/wp-content directory, $!";

  chdir($cwd) or die "Couldn't go inside $cwd directory, $!";
}

sub link_package {
  my ($dir, $webDir) = @_;
  my $wordpressDir = "$dir/wordpress";

  # Symlink the essential WordPress Directories and Files
  opendir(DH, $wordpressDir);
  my @files = readdir(DH);
  closedir(DH);

  foreach my $file (@files)
  {
    # skip unwanted files
    next if($file =~ /^\.$/);
    next if($file =~ /^\.\.$/);
    next if($file eq 'wp-content');
    next if($file eq 'readme.html');
    next if($file eq 'wp-config-sample.php');
    next if($file eq 'license.txt');

    # unlink existing symlinks
    if ( -l "$webDir/$file" ) {
      unlink "$webDir/$file"
          or warn "Failed to remove symlink $webDir/$file: $!\n";
    }

    # delete the file if its not a symlink
    if (-e "$webDir/$file") {
      unlink "$webDir/$file" or warn "Failed to remove $webDir/$file: $!";
    }

    symlink("$wordpressDir/$file", "$webDir/$file") or warn "Failed to create symlink $webDir/$file: $!\n";
  }

  # Symbolic link wp-content into opt/wordpress/wp-content
  # Workaround for WordPress-land code paths relative to opt/wordpress/wp-content
  symlink("$webDir/wp-content", "$wordpressDir/wp-content") or warn "Failed to create symlink $webDir/wp-content: $!\n";
  symlink("$webDir/wp-config.php", "$wordpressDir/wp-config.php") or warn "Failed to create symlink $webDir/wp-config.php: $!\n";
  symlink("$webDir/env.php", "$wordpressDir/env.php") or warn "Failed to create symlink $webDir/env.php: $!\n";

}

# Clean WordPress Downloads
sub clean_wordpress_archive {
  my ($dir) = @_;
  my $dh;

  if (-d $dir) {
    opendir($dh, $dir) || die "can't opendir $dir: $!";
    my @archives = grep { /^(wordpress.*\.zip)$/ && -f "$dir/$_" } readdir($dh);
    closedir $dh;

    foreach my $file (@archives) {
      if (-e "$dir/$file") {
          unlink("$dir/$file");
      }
    }
  }
}

sub update_wp_cli_bundle {
  print "Updating wp-cli/wp-cli-bundle using Composer...\n";

  my $composer = "$binDir/composer";
  my $cwd = getcwd();

  unless (-x $composer) {
    die "Error: Composer binary not found or not executable at $composer\n";
  }

  unless (-f "$web/composer.json") {
    die "Error: composer.json not found in $web\n";
  }

  chdir($web) or die "Failed to change directory to $web: $!\n";

  my $cmd = "$php $composer update wp-cli/wp-cli-bundle";
  print "Running: $cmd\n";
  system($cmd) == 0
    or die "Failed to update wp-cli/wp-cli-bundle via Composer.\n";

  print "wp-cli/wp-cli-bundle successfully updated.\n";

  chdir($cwd) or die "Failed to return to original directory $cwd: $!\n";
}
